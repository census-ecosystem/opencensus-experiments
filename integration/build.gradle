description = 'OpenCensus Java interop test client and server'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.6'
    }
}

// Currently we don't enforce checkstyle in this repo.
if (JavaVersion.current().isJava8Compatible()) {
    apply plugin: 'com.github.sherter.google-java-format'
}

repositories {
    mavenCentral()
    mavenLocal()
}

group = "io.opencensus"
version = "0.13.0-SNAPSHOT" // CURRENT_OPENCENSUS_VERSION

def opencensusVersion = "0.12.2" // LATEST_OPENCENSUS_RELEASE_VERSION
def grpcVersion = "1.9.0" // CURRENT_GRPC_VERSION
def guavaVersion = "19.0"

if (project.hasProperty("opencensusVersion")) {
    opencensusVersion = project.properties["opencensusVersion"]
}

if (project.hasProperty("grpcVersion")) {
    grpcVersion = project.properties["grpcVersion"]
}

// Google formatter works only on java8.
if (JavaVersion.current().isJava8Compatible()) {
    googleJavaFormat {
        toolVersion '1.5'
    }

    afterEvaluate {  // Allow subproject to add more source sets.
        tasks.googleJavaFormat {
            source = sourceSets*.allJava
            include '**/*.java'
        }

        tasks.verifyGoogleJavaFormat {
            source = sourceSets*.allJava
            include '**/*.java'
        }
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    compile "com.google.api.grpc:proto-google-common-protos:1.0.5",
            "io.opencensus:opencensus-api:${opencensusVersion}",
            "io.grpc:grpc-protobuf:${grpcVersion}",
            "io.grpc:grpc-stub:${grpcVersion}",
            "io.grpc:grpc-netty:${grpcVersion}",
            "com.google.guava:guava:${guavaVersion}"

            runtime "io.opencensus:opencensus-impl:${opencensusVersion}"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDir 'src'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

// Provide convenience executables for trying out the examples.
apply plugin: 'application'

startScripts.enabled = false

task interopTestServer(type: CreateStartScripts) {
    mainClassName = 'io.opencensus.interop.grpc.GrpcInteropTestServer'
    applicationName = 'GrpcInteropTestServer'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task interopTestClient(type: CreateStartScripts) {
    mainClassName = 'io.opencensus.interop.grpc.GrpcInteropTestClient'
    applicationName = 'GrpcInteropTestClient'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into('bin') {
    from(interopTestServer)
    from(interopTestClient)
    fileMode = 0755
}
